managed implementation in class ZBP_I_EVENT_WS unique;
strict ( 2 );
with draft;
extensible;
define behavior for ZI_EVENT_WS alias ZiEventWs
persistent table ZAEVENT_WS
extensible
draft table ZEVENT_WS_D
etag master LocalLastChangedAt
lock master total etag LastChangedAt
authorization master( global )
{
  field ( readonly )
   EventUUID,
   LocalCreatedBy,
   LocalCreatedAt,
   LocalLastChangedBy,
   LocalLastChangedAt,
   LastChangedAt,
   OccupiedCriticality;

  field ( numbering : managed )
   EventUUID;


  create;
  update;
  delete;

  /* Powiązanie z dzieckiem w Behaviorze */
  association _Registrations { create; with draft; }

  draft action Activate optimized;
  draft action Discard;
  draft action Edit;
  draft action Resume;
  draft determine action Prepare;

  validation validateDates on save { field startDate, endDate; create; update; }

  determination initEventValues on modify { create; }

  /* (features : instance) -> Pozwala nam dynamicznie blokować przycisk */
  /* result [1] $self      -> Akcja zwraca zaktualizowany obiekt do UI */
  action ( features : instance ) cancelEvent result [1] $self;

  mapping for ZAEVENT_WS corresponding extensible
  {
    EventUUID = event_uuid;
    EventID = event_id;
    Title = title;
    Location = location;
    StartDate = start_date;
    EndDate = end_date;
    Status = status;
    MaxSeats = max_seats;
    OccupiedSeats = occupied_seats;
    LocalCreatedBy = local_created_by;
    LocalCreatedAt = local_created_at;
    LocalLastChangedBy = local_last_changed_by;
    LocalLastChangedAt = local_last_changed_at;
    LastChangedAt = last_changed_at;
  }

}

define behavior for ZI_REG_WS alias Registration
persistent table zareg_ws
draft table zareg_ws_d
lock dependent by _Event
authorization dependent by _Event
etag master local_last_changed_at
{
  update;
  delete;

  field ( readonly ) regist_uuid, event_uuid;

  field ( numbering : managed )
   regist_uuid;
  /* Powiązanie z rodzicem */
  association _Event { with draft; }

  determination calculateOccupiedSeats on save { create; delete; }

  validation validateAvability on save { create; }

  mapping for zareg_ws
  {
    regist_uuid = regist_uuid;
    event_uuid = event_uuid;
    first_name = first_name;
    last_name = last_name;
    email = email;
    booking_date = booking_date;
    ticket_code = ticket_code;
    local_last_changed_at = local_last_changed_at;
  }
}